import com.website.gradle.Dependencies

plugins {
  id "com.diffplug.gradle.spotless" version "3.28.0"
  id 'com.google.cloud.tools.jib' version '2.5.0'
  id 'de.undercouch.download' version '4.0.0'
  id "org.gradle.playframework" version '0.10'
  id "scala"
}

repositories {
  jcenter()
  maven {
    name "lightbend-maven-release"
    url "https://repo.lightbend.com/lightbend/maven-releases"
  }
  ivy {
    name "lightbend-ivy-release"
    url "https://repo.lightbend.com/lightbend/ivy-releases"
    layout "ivy"
  }
}

ext {
  KANELA_AGENT = "kanela-agent-${Dependencies.Kanela()}.jar"
  KANELA_URL = "https://repo1.maven.org/maven2/io/kamon/kanela-agent/${Dependencies.Kanela()}/$KANELA_AGENT"
}

task downloadKanelaAgent(type: Download) {
  src KANELA_URL
  dest "${buildDir}/agents/$KANELA_AGENT"
}

play {
  platform {
    playVersion = Dependencies.PlayVersion()
    scalaVersion = Dependencies.ScalaMajor()
    javaVersion = JavaVersion.VERSION_1_8
  }
  injectedRoutesGenerator = true
  jar {
    enabled = true
  }
}

jib {
  from {
    image = "openjdk:8-jdk-slim"
  }
  to {
    image = "477016018110.dkr.ecr.us-west-2.amazonaws.com/website"
    tags = ["latest"]
  }
  container {
    entrypoint = ["/play/main/bin/main"]
    ports = ["9000"]
    // launch the app with the kanela agent
    environment = ["JAVA_OPTS":"-javaagent:/play/agents/$KANELA_AGENT".toString()]
  }
  extraDirectories {
    paths {
      path {
        from = file("$buildDir/stage")
        into = "/play"
      }
      path {
        from = file("$buildDir/agents")
        into = "/play/agents"
      }

    }
    permissions = ["/play/**/*": "777"]
  }
}

// create the distribution before building the image
stage.dependsOn(downloadKanelaAgent)
getTasks().getByName("jib").dependsOn(stage)
jibDockerBuild.dependsOn(stage)

test {
  useJUnitPlatform()
}

spotless {
  scala {
    target fileTree(rootDir) {
      include '**/*.scala'
      exclude '**/build'
    }
    scalafmt("2.5.2").configFile("$rootDir/.scalafmt.conf")
  }
}

dependencies {
  implementation Dependencies.scala()
  implementation Dependencies.guice()

  implementation Dependencies.playJsonJoda()
  testImplementation Dependencies.scalaTestPlay()
  testImplementation Dependencies.junitEngine()
  testImplementation Dependencies.junitLauncher()
  testImplementation Dependencies.junitRunner()
  implementation Dependencies.logback()

  compile group: 'io.kamon', name: 'kamon-bundle_2.13', version: '2.1.8'
  compile group: 'io.kamon', name: 'kamon-prometheus_2.13', version: '2.1.8'
}